--Must be run on same day as SalesandBudgetPerDay report because the table tsumSalesByCustomerByMonth is the source table and it is overwritten 
--with monthly data each day

declare @datestart date = '2017-03-01'
declare @dateend date = '2017-03-29'


begin try
	drop table Playground.[myop\jason.walker].jwtemp
end try
begin catch
end catch


select
	q1.Category,
	q1.FinanceReport,
	q1.CustNbr,
	q1.CustName, 
	ipc.InternalProdClassCode + ' ' + ipc.InternalProdClassDesc as ProductClass,
	bu.BusinessUnitDesc as BusinessUnit,
	case when q1.InternalProdClassID = 22 or (q1.InternalProdClassID = 4 and q1.BusinessUnitID = 3) then 'MPS'
		when q1.InternalProdClassID in (25, 26) or (q1.InternalProdClassID = 19 and q1.BusinessUnitID = 3) then 'ITS' 
		when q1.PrimaryDivisionID = 2 then 'MYOI' 
		when q1.CustName like 'Tractor Supply%' or q1.BusinessUnitID = 7 then 'Fulfillment' 
		when q1.InternalProdClassID = 27 or (q1.InternalProdClassID = 2 and q1.BusinessUnitID = 9) then 'Design Works' 
		else 'Office Products'
		end as Business_Unit_Finance,
	q1.Sales	
into Playground.[myop\jason.walker].jwtemp
from (

--Sales, no internal sales based on salesperson status
select
	'Sales(Non-Internal based on Salesperson)' as Category,
	'Y' as FinanceReport,
	c.CustNbr,
	c.CustName,
	id.BusinessUnitID,
	id.InternalProdClassID,
	sp.PrimaryDivisionID,
	sum(id.InvoicedQty * id.UnitSell) as Sales
from htdw.dbo.tInvoiceHeader ih with(nolock)
	inner join htdw.dbo.tInvoiceDetail id with(nolock)
		on ih.DWInvoiceID = id.DWInvoiceID
	inner join htdw.dbo.tCustomer c with(nolock)
		on ih.CustomerMasterID = c.CustomerMasterID and c.isCurrent = 1
	inner join htdw.dbo.tSalesperson sp with(nolock)
		on ih.PrimaryRepSPID = sp.SalespersonID
	inner join htdw.dbo.tBusinessUnit bu with(nolock)
		on id.BusinessUnitID = bu.BusinessUnitID
where
	sp.isInternal = 0
	and ih.InvDate between @datestart and @dateend
group by
	c.CustNbr,
	c.CustName,
	id.BusinessUnitID,
	id.InternalProdClassID,
	sp.PrimaryDivisionID
having
	sum(id.InvoicedQty * id.UnitSell) <> 0
	
union all

--Sales, no internal sales based on invoice status
select
	'Sales(Non-Internal based on Invoice)' as Category,
	'N' as FinanceReport,
	c.CustNbr,
	c.CustName,
	id.BusinessUnitID,
	id.InternalProdClassID,
	sp.PrimaryDivisionID,
	sum(id.InvoicedQty * id.UnitSell) as Sales
from htdw.dbo.tInvoiceHeader ih with(nolock)
	inner join htdw.dbo.tInvoiceDetail id with(nolock)
		on ih.DWInvoiceID = id.DWInvoiceID
	inner join htdw.dbo.tCustomer c with(nolock)
		on ih.CustomerMasterID = c.CustomerMasterID and c.isCurrent = 1
	inner join htdw.dbo.tSalesperson sp with(nolock)
		on ih.PrimaryRepSPID = sp.SalespersonID
	inner join htdw.dbo.tBusinessUnit bu with(nolock)
		on id.BusinessUnitID = bu.BusinessUnitID
where
	sp.isInternal = 1
	and ih.isInternalSales = 0
	and ih.InvDate between @datestart and @dateend
group by
	c.CustNbr,
	c.CustName,
	id.BusinessUnitID,
	id.InternalProdClassID,
	sp.PrimaryDivisionID
having
	sum(id.InvoicedQty * id.UnitSell) <> 0

union all

--Freight and Delivery Sales into product class 19
select
	'Freight from Header' as Category,
	'N' as FinanceReport,
	c.CustNbr,
	c.CustName,
	isnull(ih.BusinessUnitID, 0) as BusinessUnitID,
	19 as InternalProdClassID,
	sp.PrimaryDivisionID,
	SUM(ISNULL(ih.FreightAmt, 0) + ISNULL(ih.DeliveryChgAmt, 0) + ISNULL(ih.FurnDeliveryAmt, 0)) AS Sales
from htdw.dbo.tInvoiceHeader ih with(nolock)
	inner join htdw.dbo.tCustomer c with(nolock)
		on ih.CustomerMasterID = c.CustomerMasterID and c.isCurrent = 1
	inner join htdw.dbo.tSalesperson sp with(nolock)
		on ih.PrimaryRepSPID = sp.SalespersonID
where
	ih.isInternalSales = 0
	and ih.InvDate between @datestart and @dateend
group by
	c.CustNbr,
	c.CustName,
	isnull(ih.BusinessUnitID, 0),	
	sp.PrimaryDivisionID
having
	SUM(ISNULL(ih.FreightAmt, 0) + ISNULL(ih.DeliveryChgAmt, 0) + ISNULL(ih.FurnDeliveryAmt, 0)) <> 0

union all

--Add Misc Header Sales into Office Products
select
	'Misc from Header' as Category,
	'N' as FinanceReport,
	c.CustNbr,
	c.CustName,
	isnull(ih.BusinessUnitID, 0) as BusinessUnitID,
	1 as InternalProdClassID,
	sp.PrimaryDivisionID,
	SUM(ISNULL(ih.TotalHeaderSales,0) - ISNULL(ih.FreightAmt,0) - ISNULL(ih.DeliveryChgAmt,0) - ISNULL(ih.FurnDeliveryAmt,0)) AS Sales
from htdw.dbo.tInvoiceHeader ih with(nolock)
	inner join htdw.dbo.tCustomer c with(nolock)
		on ih.CustomerMasterID = c.CustomerMasterID and c.isCurrent = 1
	inner join htdw.dbo.tSalesperson sp with(nolock)
		on ih.PrimaryRepSPID = sp.SalespersonID
where
	ih.isInternalSales = 0
	and ih.InvDate between @datestart and @dateend
group by
	c.CustNbr,
	c.CustName,
	isnull(ih.BusinessUnitID, 0),	
	sp.PrimaryDivisionID
having
	SUM(ISNULL(ih.TotalHeaderSales,0) - ISNULL(ih.FreightAmt,0) - ISNULL(ih.DeliveryChgAmt,0) - ISNULL(ih.FurnDeliveryAmt,0)) <> 0

) q1
	inner join htdw.dbo.tBusinessUnit bu with(nolock)
		on q1.BusinessUnitID = bu.BusinessUnitID
	inner join htdw.dbo.tInternalProductClass ipc with(nolock)
		on q1.InternalProdClassID = ipc.InternalProdClassID