--There are two data sets
--Payments stuff
--Invoice stuff

use Playground
go

declare @datestart date = '2016-01-01'
declare @dateend date = '2016-12-31'

create table #pmt(
GLCompany varchar(25),
CustNbr varchar(25),
CustName varchar(75),
PaymentDocType varchar(25),
PaymentDocNbr varchar(25),
PaymentDate date,
ExternalDocNbr varchar(50),
BalAcctNbr varchar(25),
PaymentEntryNbr int,
PaymentAmount decimal(20,10))


insert into #pmt
select
	'RAC' as GLCompany,	
	cle.[Customer No_],
	c.Name as CustomerName,
	case when cle.[Document Type] in (0, 1) then 'Payment'
		when cle.[Document Type] = 6 then 'Refund'
		else 'Unknown'
		end as DocumentType,
	cle.[Document No_],
	cle.[Document Date],	
	cle.[External Document No_],
	cle.[Bal_ Account No_],
	cle.[Entry No_] as PaymentEntryNbr,	
	dcle.Amount	
from TNDCSQL03.NAVRep.dbo.[Rentacrate$Cust_ Ledger Entry] cle with(nolock)
	inner join TNDCSQL03.NAVRep.dbo.[Rentacrate$Detailed Cust_ Ledg_ Entry] dcle with(nolock)
		on cle.[Entry No_] = dcle.[Cust_ Ledger Entry No_]
	left join TNDCSQL03.NAVRep.dbo.[Rentacrate$Customer] c with(nolock)
		on cle.[Customer No_] = c.No_
where
	cle.[Posting Date] between @datestart and @dateend
	and cle.[Bal_ Account No_] = 'Bank4'
	and cle.[Document Type] in (0, 1, 6)
	and dcle.[Entry Type] = 1





create table #inv (
GLCompany varchar(20),
CustNbr varchar(25),
InvoiceEntryNbr int,
PaymentEntryNbr int,
InvoiceNbr varchar(25),
InvoiceDate date,
InvoiceAmount decimal(20,10))


insert into #inv

select
	'RAC' as GLCompany,
	cle.[Customer No_],
	cle.[Entry No_] as InvoiceEntryNbr,
	p.PaymentEntryNbr,
	cle.[Document No_] as InvoiceNbr,
	cle.[Document Date] as InvoiceDate,
	cle.[Sales (LCY)] as InvoiceAmount
from TNDCSQL03.NAVRep.dbo.[Rentacrate$Cust_ Ledger Entry] cle with(nolock)
	inner join #pmt p
		on cle.[Closed by Entry No_] = p.PaymentEntryNbr
where
	p.GLCompany = 'RAC'






--Shred Customer Number, Invoice
create table #shred(
	CustNbr varchar(25),
	InvoiceNbr varchar(25))

insert into #shred
select
	i.CustNbr,
	i.InvoiceNbr
from #inv i
where
	left(i.InvoiceNbr, 2) = 'SS'

--RAC Customer Number, Invoice
create table #rac(
	CustNbr varchar(25),
	InvoiceNbr varchar(25))

insert into #rac
select
	i.CustNbr,
	i.InvoiceNbr
from #inv i
where
	left(i.InvoiceNbr, 3) = 'INV'

--Shred Customber Number not in RAC
insert into #shred
select
	i.CustNbr,
	i.InvoiceNbr
from #inv i
	left outer join #rac r
		on i.CustNbr = r.CustNbr
	left outer join #shred s
		on i.CustNbr = s.CustNbr		
where
	r.CustNbr is null
	and s.CustNbr is not null
	and left(i.InvoiceNbr, 2) <> 'SS'


--Invoice
create table #invoice_data (
	GLCompany varchar(10),
	Company varchar(10),
	CustNbr varchar(25),
	CustName varchar(75),
	InvoiceNbr varchar(25),
	InvoiceDate date,
	InvoiceAmount decimal(12, 2),
	PaymentDocNbr varchar(25),
	PaymentDate date,
	PaymentAmount decimal(12, 2))


insert into #invoice_data
select
	i.GLCompany,
	case when s.CustNbr is not null and s.InvoiceNbr is not null then 'Shred'
		when r.CustNbr is not null and r.InvoiceNbr is not null then 'RAC'
		else 'RAC'
		end as Company,
	p.CustNbr,
	p.CustName,
	i.InvoiceNbr,
	i.InvoiceDate,
	i.InvoiceAmount,
	p.PaymentDocNbr,
	p.PaymentDate,
	p.PaymentAmount
from #inv i
	inner join #pmt p
		on i.PaymentEntryNbr = p.PaymentEntryNbr
	left outer join #shred s
		on i.CustNbr = s.CustNbr
		and i.InvoiceNbr = s.InvoiceNbr
	left outer join #rac r
		on i.CustNbr = r.CustNbr
		and i.InvoiceNbr = r.InvoiceNbr


--Payment Data
--select * from #pmt
	
--Invoice Data
select * from #invoice_data


drop table #pmt
drop table #inv
drop table #shred
drop table #rac
drop table #invoice_data
